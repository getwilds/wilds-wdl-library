## WILDS WDL pipeline for alternative splicing proteomics analysis.
## Combines STAR alignment, rMATS splicing detection, and JCAST protein translation
## to generate custom protein databases from RNA-seq differential splicing data.
## Based on the workflow described in the JCAST documentation:
## https://ed-lau.github.io/jcast/install.html

version 1.0

import "https://raw.githubusercontent.com/getwilds/wilds-wdl-library/refs/heads/main/modules/ww-star/ww-star.wdl" as star_tasks
import "https://raw.githubusercontent.com/getwilds/wilds-wdl-library/refs/heads/main/modules/ww-rmats-turbo/ww-rmats-turbo.wdl" as rmats_tasks
import "https://raw.githubusercontent.com/getwilds/wilds-wdl-library/refs/heads/main/modules/ww-jcast/ww-jcast.wdl" as jcast_tasks

struct SampleInfo {
    String name
    File r1
    File r2
    String group
}

struct RefGenome {
    String name
    File fasta
    File gtf
}

workflow splicing_proteomics {
  meta {
    author: "Taylor Firman"
    email: "tfirman@fredhutch.org"
    description: "WDL pipeline for alternative splicing proteomics: STAR alignment, rMATS differential splicing detection, and JCAST protein sequence translation"
    url: "https://raw.githubusercontent.com/getwilds/wilds-wdl-library/refs/heads/main/pipelines/ww-splicing-proteomics/ww-splicing-proteomics.wdl"
    outputs: {
        star_bam: "STAR alignment output BAM files for each sample",
        star_bai: "Index files for the STAR alignment BAM files",
        star_gene_counts: "Gene count files generated by STAR for each sample",
        star_log_final: "Final log files from STAR alignment for each sample",
        star_sj: "Splice junction files from STAR alignment for each sample",
        rmats_output: "Tarball containing rMATS differential splicing results",
        jcast_protein_fasta: "Combined FASTA file containing translated protein sequences from splice variants",
        jcast_output: "Tarball containing all JCAST output files including tiered results"
    }
  }

  parameter_meta {
    samples: "List of sample objects, each containing name, r1/r2 fastq files, and group assignment (group1 or group2)"
    reference_genome: "Reference genome object containing name, fasta, and gtf files"
    ensembl_gtf: "Ensembl-format GTF file required for JCAST (must have transcript_type attribute)"
    read_length: "RNA-seq read length for rMATS analysis (required)"
    read_type: "Type of reads: 'paired' or 'single' (default: paired)"
    library_type: "Library type for rMATS: 'fr-unstranded', 'fr-firststrand', or 'fr-secondstrand'"
    output_prefix: "Prefix for output file names"
    sjdb_overhang: "STAR splice junction overhang parameter (default: 100)"
    genome_sa_index_nbases: "STAR genome index parameter (typically 10-15, scales with genome size)"
    rmats_anchor_length: "Minimum nucleotides at junction ends for rMATS (default: 1)"
    rmats_novel_splice_sites: "Enable detection of unannotated splice sites in rMATS"
    rmats_cstat: "Cutoff splicing difference for rMATS statistical testing"
    jcast_min_read_count: "Minimum junction read count for JCAST translation"
    jcast_qvalue_min: "Minimum FDR q-value threshold for JCAST"
    jcast_qvalue_max: "Maximum FDR q-value threshold for JCAST"
    jcast_splice_types: "Comma-separated splice types for JCAST (MXE,RI,SE,A3SS,A5SS). Empty for all."
    star_cpu: "Number of CPU cores for STAR alignment tasks"
    star_memory_gb: "Memory allocation in GB for STAR alignment tasks"
    rmats_cpu: "Number of CPU cores for rMATS task"
    rmats_memory_gb: "Memory allocation in GB for rMATS task"
    jcast_cpu: "Number of CPU cores for JCAST task"
    jcast_memory_gb: "Memory allocation in GB for JCAST task"
  }

  input {
    Array[SampleInfo] samples
    RefGenome reference_genome
    File ensembl_gtf
    Int read_length
    String read_type = "paired"
    String library_type = "fr-unstranded"
    String output_prefix = "splicing_proteomics"
    Int sjdb_overhang = 100
    Int genome_sa_index_nbases = 14
    Int rmats_anchor_length = 1
    Boolean rmats_novel_splice_sites = false
    Float rmats_cstat = 0.0001
    Int jcast_min_read_count = 1
    Float jcast_qvalue_min = 0
    Float jcast_qvalue_max = 1
    String jcast_splice_types = ""
    Int star_cpu = 8
    Int star_memory_gb = 64
    Int rmats_cpu = 4
    Int rmats_memory_gb = 16
    Int jcast_cpu = 2
    Int jcast_memory_gb = 8
  }

  # Step 1: Build STAR genome index
  call star_tasks.build_index { input:
      reference_fasta = reference_genome.fasta,
      reference_gtf = reference_genome.gtf,
      sjdb_overhang = sjdb_overhang,
      genome_sa_index_nbases = genome_sa_index_nbases,
      cpu_cores = star_cpu,
      memory_gb = star_memory_gb
  }

  # Step 2: Align all samples with STAR
  scatter (sample in samples) {
    String sample_name = sample.name
    String sample_group = sample.group

    call star_tasks.align_two_pass { input:
        star_genome_tar = build_index.star_index_tar,
        r1 = sample.r1,
        r2 = sample.r2,
        name = sample.name + "." + reference_genome.name,
        sjdb_overhang = sjdb_overhang,
        cpu_cores = star_cpu,
        memory_gb = star_memory_gb
    }
  }

  # Separate BAM files by group for rMATS
  # Group 1 samples
  Array[Pair[String, File]] group_bam_pairs = zip(sample_group, align_two_pass.bam)

  scatter (pair in group_bam_pairs) {
    if (pair.left == "group1") {
      File group1_bam_file = pair.right
    }
    if (pair.left == "group2") {
      File group2_bam_file = pair.right
    }
  }

  Array[File] group1_bams = select_all(group1_bam_file)
  Array[File] group2_bams = select_all(group2_bam_file)

  # Step 3: Run rMATS differential splicing analysis
  call rmats_tasks.rmats { input:
      gtf_file = reference_genome.gtf,
      sample1_bams = group1_bams,
      sample2_bams = group2_bams,
      read_length = read_length,
      read_type = read_type,
      library_type = library_type,
      output_name = output_prefix + "_rmats",
      anchor_length = rmats_anchor_length,
      novel_splice_sites = rmats_novel_splice_sites,
      cstat = rmats_cstat,
      cpu_cores = rmats_cpu,
      memory_gb = rmats_memory_gb
  }

  # Step 4: Run JCAST to translate splice junctions to protein sequences
  call jcast_tasks.jcast { input:
      rmats_directory = rmats.output_directory,
      gtf_file = ensembl_gtf,
      genome_fasta = reference_genome.fasta,
      output_name = output_prefix + "_jcast",
      min_read_count = jcast_min_read_count,
      qvalue_min = jcast_qvalue_min,
      qvalue_max = jcast_qvalue_max,
      splice_types = jcast_splice_types,
      cpu_cores = jcast_cpu,
      memory_gb = jcast_memory_gb
  }

  output {
    Array[File] star_bam = align_two_pass.bam
    Array[File] star_bai = align_two_pass.bai
    Array[File] star_gene_counts = align_two_pass.gene_counts
    Array[File] star_log_final = align_two_pass.log_final
    Array[File] star_sj = align_two_pass.sj_out
    File rmats_output = rmats.output_directory
    File jcast_protein_fasta = jcast.output_fasta
    File jcast_output = jcast.output_directory
  }
}
