version 1.0

import "https://raw.githubusercontent.com/getwilds/wilds-wdl-library/refs/heads/main/modules/ww-star/ww-star.wdl" as star_tasks
import "https://raw.githubusercontent.com/getwilds/wilds-wdl-library/refs/heads/main/modules/ww-deseq2/ww-deseq2.wdl" as deseq2_tasks
import "https://raw.githubusercontent.com/getwilds/wilds-wdl-library/refs/heads/add-star-deseq/modules/ww-rseqc/ww-rseqc.wdl" as rseqc_tasks
import "https://raw.githubusercontent.com/getwilds/wilds-wdl-library/refs/heads/add-star-deseq/modules/ww-bedparse/ww-bedparse.wdl" as bedparse_tasks

struct SampleInfo {
    String name
    File r1
    File r2
    String condition
}

struct RefGenome {
    String name
    File fasta
    File gtf
}

workflow star_deseq2 {
  meta {
    author: "Taylor Firman"
    email: "tfirman@fredhutch.org"
    description: "WDL workflow for RNA-seq alignment via STAR and DESeq2 differential expression analysis"
    url: "https://github.com/getwilds/ww-star-deseq2"
    outputs: {
        star_bam: "STAR alignment output BAM files for each sample",
        star_bai: "Index files for the STAR alignment BAM files",
        star_gene_counts: "Gene count files generated by STAR for each sample",
        star_log_final: "Final log files from STAR alignment for each sample",
        star_log_progress: "Progress log files from STAR alignment for each sample",
        star_log: "Main log files from STAR alignment for each sample",
        star_sj: "Splice junction files from STAR alignment for each sample",
        rseqc_qc_summary: "Quality control summary reports produced by RSeQC for each sample",
        combined_counts_matrix: "Matrix file containing combined gene counts from all samples",
        sample_metadata: "Metadata file containing sample information and conditions",
        deseq2_all_results: "Complete DESeq2 differential expression results for all genes",
        deseq2_significant_results: "Filtered DESeq2 results containing only significant differentially expressed genes",
        deseq2_normalized_counts: "Normalized count values produced by DESeq2",
        deseq2_pca_plot: "Principal Component Analysis plot of samples based on expression patterns",
        deseq2_volcano_plot: "Volcano plot showing log fold change vs. statistical significance",
        deseq2_heatmap: "Heatmap of differentially expressed genes across samples"
    }
  }

  parameter_meta {
    samples: "List of sample objects, each containing name, r1/r2 fastq files, and condition information"
    reference_genome: "Optional reference genome object containing name, fasta, and gtf files (if not provided, GRCh38 will be downloaded)"
    reference_level: "Reference level for DESeq2 contrast (e.g., 'control' when comparing treatment vs. control)"
    contrast: "DESeq2 contrast string in the format 'condition,treatment,control'"
    star_cpu: "Number of CPU cores for STAR alignment tasks"
    star_memory_gb: "Memory allocation in GB for STAR alignment tasks"
  }

  input {
    Array[SampleInfo] samples
    RefGenome reference_genome
    String reference_level = ""
    String contrast = ""
    Int star_cpu = 8
    Int star_memory_gb = 64
  }

  call star_tasks.build_index { input:
      reference_fasta = reference_genome.fasta,
      reference_gtf = reference_genome.gtf,
      cpu_cores = star_cpu,
      memory_gb = star_memory_gb
  }

  # Convert GTF to BED for RSeQC
  call bedparse_tasks.gtf2bed { input:
      gtf_file = reference_genome.gtf
  }

  scatter (sample in samples) {
    String sample_name = sample.name
    String sample_condition = sample.condition

    call star_tasks.align_two_pass { input:
        star_genome_tar = build_index.star_index_tar,
        r1 = sample.r1,
        r2 = sample.r2,
        name = sample.name + "." + reference_genome.name,
        cpu_cores = star_cpu,
        memory_gb = star_memory_gb
    }

    call rseqc_tasks.run_rseqc { input:
        sample_name = sample.name,
        bam_file = align_two_pass.bam,
        bam_index = align_two_pass.bai,
        ref_bed = gtf2bed.bed_file
    }
  }

  call deseq2_tasks.combine_count_matrices { input:
      gene_count_files = align_two_pass.gene_counts,
      sample_names = sample_name,
      sample_conditions = sample_condition
  }

  call deseq2_tasks.run_deseq2 { input:
      counts_matrix = combine_count_matrices.counts_matrix,
      sample_metadata = combine_count_matrices.sample_metadata,
      reference_level = reference_level,
      contrast = contrast
  }

  output {
    Array[File] star_bam = align_two_pass.bam
    Array[File] star_bai = align_two_pass.bai
    Array[File] star_gene_counts = align_two_pass.gene_counts
    Array[File] star_log_final = align_two_pass.log_final
    Array[File] star_log_progress = align_two_pass.log_progress
    Array[File] star_log = align_two_pass.log
    Array[File] star_sj = align_two_pass.sj_out
    Array[File] rseqc_qc_summary = run_rseqc.rseqc_summary
    File combined_counts_matrix = combine_count_matrices.counts_matrix
    File sample_metadata = combine_count_matrices.sample_metadata
    File deseq2_all_results = run_deseq2.deseq2_results
    File deseq2_significant_results = run_deseq2.deseq2_significant
    File deseq2_normalized_counts = run_deseq2.deseq2_normalized_counts
    File deseq2_pca_plot = run_deseq2.deseq2_pca_plot
    File deseq2_volcano_plot = run_deseq2.deseq2_volcano_plot
    File deseq2_heatmap = run_deseq2.deseq2_heatmap
  }
}
