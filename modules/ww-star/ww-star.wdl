## WILDS WDL for performing RNA-seq alignment using STAR's two-pass methodology.
## Designed to be a modular component within the WILDS ecosystem that can be used
## independently or integrated with other WILDS workflows.

version 1.0

task build_index {
  meta {
    author: "Taylor Firman"
    email: "tfirman@fredhutch.org"
    description: "Task for building the STAR index files from fasta/gtf."
    outputs: {
        star_index_tar: "Compressed tarball containing the STAR genome index for future alignment steps"
    }
  }

  parameter_meta {
    reference_fasta: "Reference genome FASTA file"
    reference_gtf: "Reference genome GTF annotation file"
    sjdb_overhang: "Length of the genomic sequence around the annotated junction to be used in constructing the splice junctions database"
    genome_sa_index_nbases: "Length (bases) of the SA pre-indexing string, typically between 10-15 (scales with genome size)"
    memory_gb: "Memory allocated for the task in GB"
    cpu_cores: "Number of CPU cores allocated for the task"
  }

  input {
    File reference_fasta
    File reference_gtf
    Int sjdb_overhang = 100
    Int genome_sa_index_nbases = 14
    Int memory_gb = 64
    Int cpu_cores = 8
  }

  command <<<
    set -eo pipefail
    
    mkdir star_index

    echo "Building STAR index..."
    STAR \
      --runMode genomeGenerate \
      --runThreadN ~{cpu_cores} \
      --genomeDir star_index \
      --genomeFastaFiles "~{reference_fasta}" \
      --sjdbGTFfile "~{reference_gtf}" \
      --sjdbOverhang ~{sjdb_overhang} \
      --genomeSAindexNbases ~{genome_sa_index_nbases}

    # Compress the index directory into a tarball
    tar -czf star_index.tar.gz star_index/*

    # Clean up the index directory to save space
    rm -rf star_index
  >>>

  output {
    File star_index_tar = "star_index.tar.gz"
  }

  runtime {
    docker: "getwilds/star:2.7.6a"
    memory: "~{memory_gb} GB"
    cpu: cpu_cores
  }
}

task align_two_pass {
  meta {
    author: "Taylor Firman"
    email: "tfirman@fredhutch.org"
    description: "Task for aligning RNA-seq reads using STAR's two-pass technique."
    outputs: {
        bam: "Aligned reads in sorted BAM format",
        bai: "Index file for the aligned BAM file",
        gene_counts: "Gene-level read counts generated by STAR",
        log_final: "Final summary log of the STAR alignment process",
        log_progress: "Progress log containing time and resource usage during alignment",
        log: "Main log file from STAR containing detailed alignment information",
        sj_out: "Splice junction file with coordinates of detected splice junctions"
    }
  }

  parameter_meta {
    star_genome_tar: "Compressed tarball containing STAR genome index"
    r1: "FASTQ file for read 1"
    r2: "FASTQ file for read 2"
    name: "Sample name to include in output filenames"
    sjdb_overhang: "Length of the genomic sequence around the annotated junction"
    memory_gb: "Memory allocated for the task in GB"
    cpu_cores: "Total number of CPU cores allocated for the task"
    star_threads: "Number of threads to use for STAR alignment (subset of cpu_cores)"
  }

  input {
    File star_genome_tar
    File r1
    File r2
    String name
    Int sjdb_overhang = 100
    Int memory_gb = 62
    Int cpu_cores = 8
    Int star_threads = 6
  }

  command <<<
    set -eo pipefail

    echo "Extracting STAR reference..."
    tar -xvf "~{star_genome_tar}"

    echo "Starting STAR alignment..."
    STAR \
      --genomeDir star_index \
      --readFilesIn "~{r1}" "~{r2}" \
      --runThreadN ~{star_threads} \
      --readFilesCommand zcat \
      --sjdbOverhang ~{sjdb_overhang} \
      --outSAMtype BAM SortedByCoordinate \
      --twopassMode Basic \
      --outTmpDir _STARtmp \
      --outFileNamePrefix "./" \
      --quantMode GeneCounts \
      --quantTranscriptomeBAMcompression 5 

    # Clean up temporary directories
    rm -rf star_index _STARpass1 _STARgenome 2>/dev/null || true

    mv Aligned.sortedByCoord.out.bam \
      "~{name}.Aligned.sortedByCoord.out.bam"
    mv ReadsPerGene.out.tab "~{name}.ReadsPerGene.out.tab"
    mv Log.final.out "~{name}.Log.final.out"
    mv Log.progress.out "~{name}.Log.progress.out"
    mv Log.out "~{name}.Log.out"
    mv SJ.out.tab "~{name}.SJ.out.tab"

    samtools index "~{name}.Aligned.sortedByCoord.out.bam"
  >>>

  output {
    File bam = "~{name}.Aligned.sortedByCoord.out.bam"
    File bai = "~{name}.Aligned.sortedByCoord.out.bam.bai"
    File gene_counts = "~{name}.ReadsPerGene.out.tab"
    File log_final = "~{name}.Log.final.out"
    File log_progress = "~{name}.Log.progress.out"
    File log = "~{name}.Log.out"
    File sj_out = "~{name}.SJ.out.tab"
  }

  runtime {
    docker: "getwilds/star:2.7.6a"
    memory: "~{memory_gb} GB"
    cpu: cpu_cores
  }
}
